#version 430
#extension GL_KHR_vulkan_glsl : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_debug_printf : enable

#include "acceptbools.glsl"
#include "debug.glsl.h"
#include "../../../../foray/src/shaders/common/viridis.glsl" // TODO: Remove me after testing

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(rgba16f, binding = 0) uniform readonly image2D PrimaryInput;

layout(rgba16f, binding = 1) uniform readonly image2D GbufferPositions;
layout(rgba16f, binding = 2) uniform readonly image2D HistoryGbufferPositions;

layout(rgba16f, binding = 3) uniform readonly image2D GbufferNormals;
layout(rgba16f, binding = 4) uniform readonly image2D HistoryGbufferNormals;

layout(rg16f, binding = 5) uniform readonly image2D GbufferMotionVec;

layout(rgba16f, binding = 6) uniform image2DArray AccumulatedColor; //ReadWrite access

layout(r8ui, binding = 7) uniform writeonly uimage2D AcceptBools; // For bilinear kernel, set bits # 0...3 for accept values

layout(rgba16f, binding = 8) uniform writeonly image2D DebugOutput;

layout(push_constant) uniform push_constant_t
{
    // Read array index
    uint ReadIdx;
    // Write array index
    uint WriteIdx;
    // Maximum position difference (Default 0.15)
    float MaxPositionDifference;
    // Maximum deviation of the sinus of previous and current normal (Default 0.05)
    float MaxNormalDeviation;
    // Combined Weight Threshhold (Default 0.01)
    float WeightThreshhold;
    // Minimum weight assigned to new data
    float MinNewDataWeight;
    uint EnableHistory;
    uint DebugMode;
} PushC;

bool testInsideScreen(in ivec2 samplePos, in ivec2 renderSize)
{
    return samplePos.x >= 0 && samplePos.x < renderSize.x && samplePos.y >= 0 && samplePos.y < renderSize.y;
}

bool testNormalDeviation(in vec3 currNormal, in vec3 prevNormal)
{
    return (1 - dot(currNormal, prevNormal)) <= PushC.MaxNormalDeviation;
}

bool testPositions(in vec3 currPositions, in vec3 prevPositions)
{
    vec3 difference = prevPositions - currPositions;

    float maxDiffSquared = PushC.MaxPositionDifference * PushC.MaxPositionDifference;

    return dot(difference, difference) < maxDiffSquared;
}

void main()
{
    ivec2 currTexel = ivec2(gl_GlobalInvocationID.xy);

    ivec2 renderSize = imageSize(GbufferMotionVec);

    vec2 motionVec = imageLoad(GbufferMotionVec, currTexel).xy;

    vec2 prevTexel = currTexel + motionVec * imageSize(PrimaryInput).xy;

    vec2 prevPosSubPixel = fract(prevTexel);
    
    vec3 position = imageLoad(GbufferPositions, currTexel).xyz;

    vec3 currColor = imageLoad(PrimaryInput, currTexel).rgb;

    vec3 currNormal = imageLoad(GbufferNormals, currTexel).rgb;

    uint acceptBools = 0;

    vec3 prevColor = vec3(0);
    float historyLength = 0.f;
    float summedWeight = 0.f;

    if (PushC.EnableHistory > 0)
    { // Read history data w/ bilinear interpolation
    	for(int y = 0; y <= 1; y++) {
    		for(int x = 0; x <= 1; x++) {
                // current position
    			ivec2 samplePos    = ivec2(prevTexel + ivec2(x, y));
                // load previous Position
    			vec3 prevPosition    = imageLoad(HistoryGbufferPositions, samplePos).xyz;
                // load previous Normal
    			vec3  prevNormal   = imageLoad(HistoryGbufferNormals, samplePos).rgb;

    			bool accept = true;
    			accept = accept && testInsideScreen(samplePos, renderSize); // discard outside viewport
    			accept = accept && testNormalDeviation(currNormal, prevNormal); // discard if normal deviates too far (18 degrees max)     
                accept = accept && testPositions(position, prevPosition); // Discard if world space positions differ to much

                writeAcceptBool(acceptBools, ivec2(x, y), accept);

    			if(accept) {
    				float weight = (x == 0 ? (1.0 - prevPosSubPixel.x) : prevPosSubPixel.x)
    					    * (y == 0 ? (1.0 - prevPosSubPixel.y) : prevPosSubPixel.y); // bilinear weight

                    vec4 colorAndHistoryLength = imageLoad(AccumulatedColor, ivec3(samplePos, PushC.ReadIdx)) * weight;
                    // Accumulate Color
    				prevColor   += colorAndHistoryLength.rgb;
                    // Accumulate History
    				historyLength += colorAndHistoryLength.a;
                    // Accumulate Weights
    				summedWeight += weight;
    			}
    		}
        }
    }

    imageStore(AcceptBools, currTexel, uvec4(acceptBools, 0, 0, 0));

    if (summedWeight > PushC.WeightThreshhold)
    {
        // Alpha values: [0...1], where 0 == only history data, 1 == only new data
        // Calculate mean for Colors
        prevColor /= summedWeight;
        // Calculate mean for History
        historyLength /= summedWeight;
        
        // Temporal accumulation factor a alpha for drop stale history information

        float rawHistoryAlpha = 1.f / (historyLength + 1.f);
        float colorAlpha = max(PushC.MinNewDataWeight, rawHistoryAlpha);

        // Mix everything together and store the images

        vec4 accuColorPlusHistlen = vec4(mix(prevColor, currColor, colorAlpha), min(64, historyLength + 1.f));
        imageStore(AccumulatedColor, ivec3(currTexel, PushC.WriteIdx), accuColorPlusHistlen);

        if (PushC.DebugMode == DEBUG_PREPROCESS_OUT)
        {
            imageStore(DebugOutput, currTexel, vec4(mix(prevColor, currColor, colorAlpha), 1.f));
        }
        if (PushC.DebugMode == DEBUG_PREPROCESS_ALPHA)
        {
            imageStore(DebugOutput, currTexel, vec4(colorAlpha, 0.f, 0.f, 1.f));
        }
    }
    // If weight is to small dont mix the colors
    else
    {
        imageStore(AccumulatedColor, ivec3(currTexel, PushC.WriteIdx), vec4(currColor, 1.f));
        if (PushC.DebugMode == DEBUG_PREPROCESS_OUT)
        {
            imageStore(DebugOutput, currTexel, vec4(currColor, 1.f));
        }
        if (PushC.DebugMode == DEBUG_PREPROCESS_ALPHA)
        {
            imageStore(DebugOutput, currTexel, vec4(0.f, 0.f, 0.f, 1.f));
        }
    }
    if (PushC.DebugMode == DEBUG_PREPROCESS_ACCEPTS)
    {
        float accept0 = readAcceptBool(acceptBools, ivec2(0, 0)) ? 0.25f : 0.f;
        float accept1 = readAcceptBool(acceptBools, ivec2(1, 0)) ? 0.25f : 0.f;
        float accept2 = readAcceptBool(acceptBools, ivec2(0, 1)) ? 0.25f : 0.f;
        float accept3 = readAcceptBool(acceptBools, ivec2(1, 1)) ? 0.25f : 0.f;
        imageStore(DebugOutput, currTexel, vec4(viridis(accept0 + accept1 + accept2 + accept3), 1));
    }
}